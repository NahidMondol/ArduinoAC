#include<LiquidCrystal.h>

// designated I/O pins for arduino
const int tempUp = 8;
const int tempDown = 9;
const int inFan = 10;
const int outFan = 11;
int tempSensor = A0;
LiquidCrystal lcd(2,3,4,5,6,7);

int *initialTemp;
int requestedTemp, curTemp;
int currentMilli;
const char degreeSymbol = char(223);

// Values needed for Stein & Hart Equation
double resistance = 10000;
double a = 1.009249522e-03, b = 2.378405444e-04, c = 2.019202697e-07;

// Set tempurature clearances when activating the fan
int clearanceF = 3, clearanceC = 1, clearance;

// TRUE FOR FAHRENHEIT
// FALSE FOR CELSIUS
boolean fahrOverCelsius = true;

void setup()
{
  Serial.begin(9600);
  // Assign components
  pinMode(tempUp, INPUT);
  pinMode(tempDown, INPUT);
  pinMode(tempSensor, INPUT);
  pinMode(inFan, OUTPUT);
  pinMode(outFan, OUTPUT);
  // 16x2 display 
  lcd.begin(16,2);

  // Set the initial tempurature to the current tempurature when starting
  // up the program
  curTemp = currentTemp(resistance, a, b, c);
  initialTemp = &curTemp;
  requestedTemp = *initialTemp;

  setFanClearance(fahrOverCelsius);
}

void loop()
{
  curTemp = currentTemp(resistance, a, b, c);
  fanIntakeState(curTemp, requestedTemp, clearance);

  currentMilli = millis();
  changeTemp();
  setLCD(curTemp);
}

// Change the requested tempurature by pressing a button on the breadboard
void changeTemp()
{
  if(digitalRead(tempUp) == HIGH)
    {
      delay(250);
      requestedTemp++;
    }
    if(digitalRead(tempDown) == HIGH)
    {
      delay(250);
      requestedTemp--;
    }
}


// https://en.wikipedia.org/wiki/Steinhart%E2%80%93Hart_equation
// Set return type as double if needed
int currentTemp(double resistance, double a, double b, double c)
{
  double Vo, temp;
  
  Vo = analogRead(tempSensor);
  resistance = (resistance * ((1023.0 / Vo) - 1.0));
  temp = ( (1.0) / ( (a) + (b * log(resistance)) + (c * (pow(log(resistance), 3))) ) );
  temp = temp - 273.15;
  if(fahrOverCelsius)
  {
    temp = (temp * 9.0)/ 5.0 + 32.0;
  }
  
  return temp;
}

// Sets the lcd screen to show the requested temp on the first row
// and the current temp to the bottom row of the lcd
void setLCD(int curTemp)
{
  // First Row
  lcd.setCursor(1,0);
  lcd.print("Req Temp: ");
  lcd.print(requestedTemp);
  lcd.print(degreeSymbol);
  switch(fahrOverCelsius)
  {
    case true: lcd.print("F"); break;
    case false: lcd.print("C"); break;
  }
  
  // Second Row
  lcd.setCursor(1,2);
  lcd.print("Cur Temp: ");
  if(currentMilli % 500 == 0) {
    lcd.print(curTemp);
    lcd.print(degreeSymbol);
     switch(fahrOverCelsius)
     {
      case true: lcd.print("F"); break;
      case false: lcd.print("C"); break;
    }
  }
}

// Set whether the intake fan is active or inactive based on
// current tempurature and requested tempurature
void fanIntakeState(int curTemp, int requestedTemp, int clearance)
{
  if(curTemp - clearance >= requestedTemp)
  {
    digitalWrite(inFan, HIGH);
    digitalWrite(outFan, HIGH);
  }
  else
  {
    digitalWrite(inFan, LOW);
    digitalWrite(outFan, LOW);
  }
}

// If the mode is set as celsius, change the
// clearance tempurature before the fan turns on
void setFanClearance(boolean fahrOverCelsius)
{
  switch(fahrOverCelsius)
  {
    case true: clearance = clearanceF; break;
    case false: clearance = clearanceC; break;
  }
}
